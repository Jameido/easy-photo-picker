apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '0.0.4'
group 'dev.jameido'

bintray {

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty('bintrayUser')
    key = properties.getProperty('bintrayApiKey')
    publications = ['Development']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'android-libraries'
        name = 'easypic'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Jameido/easypic-android.git'
        version {
            name = this.version
            desc = "Easy Pic ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}

// Create the pom configuration:
def pomConfig = {

    developers {
        developer {
            id "jameido"
            name "Luca Rossi"
            email "rossiluca.dev@gmail.com"
        }
    }

    licenses {
        license {
            name "The MIT License"
            url "https://github.com/Jameido/easypic-android/blob/dev/LICENSE"
            distribution "repo"
        }
    }

    scm {
        connection "https://github.com/Jameido/easypic-android.git"
        developerConnection "https://github.com/Jameido/easypic-android.git"
        url "https://github.com/Jameido/easypic-android"

    }
}

publishing {
    publications {
        Development(MavenPublication) {

            artifact("$buildDir/outputs/aar/easypic-release.aar")
            groupId this.group
            artifactId 'easypic'
            version this.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An Android library that makes taking pictures from camera or gallery easy as taking a glass of water.')
                root.appendNode('name', 'easypic')
                root.appendNode('url', 'https://github.com/Jameido/easypic-android')
                root.children().last() + pomConfig
            }

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}